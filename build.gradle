plugins {
	id 'java'
	id 'org.springframework.boot' version '2.6.6'
	id 'io.spring.dependency-management' version '1.1.4'
	id "org.openapi.generator" version "5.3.0"//7.7.0
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	compileOnly 'io.swagger:swagger-annotations:1.6.5'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

task buildApiDoc(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
	generatorName.set("html2")
	inputSpec.set("$rootDir/src/main/resources/api-schema.yml")
	outputDir.set(layout.buildDirectory.dir("apidoc").get().toString())
}

task buildSpringServer(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
	generatorName.set("spring")
	inputSpec.set("$rootDir/src/main/resources/api-schema.yml")
	outputDir.set(layout.buildDirectory.dir("spring").get().toString())
	apiPackage.set("com.example.todoapi.controller")
	modelPackage.set("com.example.todoapi.model")
	configOptions.set([
			interfaceOnly: "true",
	])
}

compileJava.dependsOn tasks.buildSpringServer
sourceSets.main.java.srcDir layout.buildDirectory.dir("spring/src/main/java").get().toString()